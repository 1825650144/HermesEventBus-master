apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = "com.lhfboy"
version = "0.0.2"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1002
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    api 'org.greenrobot:eventbus:3.1.1'
//    implementation 'xiaofei.library:hermes:0.7.0'
//    implementation 'xiaofei.library:concurrent-utils:0.1.4'
    implementation 'com.google.code.gson:gson:2.8.5'

}


def siteUrl = 'https://github.com/Xiaofei-it/HermesEventBus/blob/master/README-zh-CN.md'    // 项目主页
def gitUrl = 'https://github.com/Xiaofei-it/HermesEventBus.git' // 项目的git地址

install {
    repositories.mavenInstaller {
        // 生成pom.xml
        pom {
            project {
                packaging 'aar' // 打包的类型
                name 'HermesEventBus-master' // 项目名称
                url siteUrl // 项目的主页
                // 配置开源证书信息
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 配置开发者信息
                developers {
                    developer {
                        id 'lhfboy'
                        name 'haifeng.li'
                        email '18256550144@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// 生成对应的source.jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成java doc文件
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// 生成对应的javadoc.jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// 读取配置的bintray用户的信息
Properties properties = new Properties()
boolean isHasFile = false
if (project.rootProject.file('local.properties') != null){
    isHasFile = true
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

// 配置bintray的信息
bintray {
    user = isHasFile ? properties.getProperty("bintray.user") : System.getenv("bintray.user")
    key = isHasFile ? properties.getProperty("bintray.apikey") : System.getenv("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven" // 在bintray上边建立的仓库名字
        name = "HermesEventBus" // 在bintray上Package名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        labels = ['android']  // 设置标签
        licenses = ["Apache-2.0"]
        publish = true
    }
}
